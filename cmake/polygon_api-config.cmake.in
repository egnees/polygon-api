# CMake package config file for polygon-api library.
#
# The following target is imported:
#
#   polygon_api::polygon_api
#
# Type of target to import (static or shared) is determined by the following algorithm:
#
#   1. if POLYGON_API_SHARED_LIBS is defined and:
#     1.1 is true, then import shared library target or issue error
#     1.2 is false, then import static library target or issue error
#
#   2. if BUILD_SHARED_LIBS is true and file containing shared library target definition exists,
#      then import shared library target
#
#   3. if file containing static library target definition exists, then import static library target
#
#   4. import shared library target or issue error

@PACKAGE_INIT@

macro(import_targets type)
    if(NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/polygon_api-${type}-targets.cmake")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "polygon_api ${type} libraries were requested but not found")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND OFF)
        return()
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/polygon_api-${type}-targets.cmake")
endmacro()

if(NOT TARGET polygon_api::polygon_api)
    set(type "")

    if(DEFINED POLYGON_API_SHARED_LIBS)
        if(POLYGON_API_SHARED_LIBS)
            set(type "shared")
        else()
            set(type "static")
        endif()
    elseif(BUILD_SHARED_LIBS AND EXISTS "${CMAKE_CURRENT_LIST_DIR}/polygon_api-shared-targets.cmake")
        set(type "shared")
    elseif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/polygon_api-static-targets.cmake")
        set(type "static")
    else()
        set(type "shared")
    endif()

    import_targets(${type})
    check_required_components(polygon_api)
    message("-- Found ${type} polygon_api (version ${${CMAKE_FIND_PACKAGE_NAME}_VERSION})")
endif()